#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0 VDC,uppercase,10,10
#BMP-AGI
# THIS PROGRAM'S GOAL IS TO CONVERT 256X256 IMAGES TO 512X256 VDC FORMAT
# PREREQ IS THAT NO COLOR CLASHES ARE PRESENT IN THE FILES
#  IF THERE ARE, RESULT WILL DIFFER FROM EXPECTATION :-)

# THIS READS 4-BIT COLOR FILES. 1 BYTE CONTAINS 2 PIXELS

# SPECIAL THING: BLACK IS ALWAYS BG-COLOR IF PRESENT


10 GRAPHIC 0
# DEFINE FUNCTIONS
20 GOSUB 910
30 BS=FN W(45)
40 BE=FN W(4624)
44 VS=FN W(57)
45 VE=FN W(2567)

# DB:DATA BEGIN
# DE:DATA END
50 DB=BE+1:DE=DB+8000


60 PRINT "BASIC FROM "BS" TO "BE"."
65 PRINT " VARS FROM "VS" TO "VE"."
70 PRINT "LEAVES "FRE(0)" BYTES IN BANK 0."
75 PRINT "   AND "FRE(1)" BYTES IN BANK 1."
80 DD=PEEK(186)

90 PRINT "INSERT DATADISK":GETKEY I$

100 BLOAD "VDCBASICAC6.BIN",B0,U(DD):SYS DEC("AC6")

110 AP=16000

120 RGO 28,16:REM 64K VRAM
130 RGO 25,128:REM BITMAP MODE ON
# 2 SCANLINES PER CHAR, 156 SCANLINES (EACH VAL +1)
140 RGW 0,127:RGW 4,155:RGW 6,100:RGW 7,140:RGW 9,1
150 RGW 36,0
155 RGW 1,64

160 ATTR AP:DISP 0


170 CATALOG U(DD):PF$=""
180 PRINT "FILENAME";:INPUT PF$
190 IF LEN(PF$)=0 THEN PRINT "BYE.":END

200 PRINT "LOADING "PF$" ... ";
210 BLOAD (PF$),B0,P(DB),U(DD)
220 PRINT "DONE"
230 VMF 0,0,24000: REM CLEAR VRAM
240 H1=PEEK(DB):H2=PEEK(DB+1)
250 IF (CHR$(H1)+CHR$(H2)) = "BM" THEN GOSUB 300

260 SLOW:BANK 15
270 PF$=PF$+".VDC":PRINT "SAVING "PF$"..."
280 BSAVE (PF$),B0,U(DD),P(DB) TO P(DB+24000)

290 GOTO 160

##################
# BITMAP ROUTINE #
##################

# HEADER
300 PRINT "BITMAP"
310 IS=FN W(DB+2)
320 PRINT "BFSIZE"IS
330 OS=FN W4(DB+10)
340 PRINT "BFOFFBITS"OS

# INFORMATIONBLOCK
#1030 PRINT "BISIZE"FN W4(DB+14)
350 BW=FN W4(DB+18)
360 PRINT "BIWIDTH"BW
370 BH=FN W4(DB+22)
380 PRINT "BIHEIGHT"BH
390 BC=FN W(DB+28)
400 PRINT "BIBITCOUNT"BC
410 IF BC<=8 THEN PRINT " INDICED COLORS"
420 IF BC >8 THEN PRINT " COLOR DEPTH "BC" NOT SUPPORTED. THE END.":END
430 PRINT "BICOMPRESSION"FN W4(DB+30)
440 PRINT "BISIZEIMAGE"FN W4(DB+34)
#1090 PRINT "BIXPELSPERMETER"FN W4(DB+38)
#1100 PRINT "BIYPELSPERMETER"FN W4(DB+42)
450 CU=FN W4(DB+46)
460 PRINT "BICLRUSED"CU
470 PRINT "BICLRIMPORTANT"FN W4(DB+50)

# PALETTE
480 IF CU<>0 THEN PRINT " PALETTE WITH "CU" ENTRIES":GOTO 510
490 IF BC<=8 THEN PRINT " PALETTE WITH "BC" ENTRIES":ELSE PRINT " NO PALETTE"

500 DIM P$(7),P(7)

# IMAGEDATA
# ID=IMAGE DATA (IE INPUT)
#  1 BYTE PER 1 PIXEL -> 320 X 200
# VP=CURRENT ATTRIBUTE RAM POSITION
#  1 BYTE PER 8X2 PIXELS -> 80 X 100
# VR=CURRENT SCREEN RAM POSITION
#  1 BYTE PER 2X1 PIXELS -> 320 X 200

510 BANK0:ID=DB+OS:VP=AP:VR=0
520 S=TI:FAST
530 FOR Y=0 TO (BH/2)-1
540  FOR X=0 TO (BW/4)-1

550   BB=0:B=0

# SX/SY:SCREEN-RAM X AND Y COORDINATE
# AX/AY:ATTRIBUTE-RAM X AND Y COORDINATE
#1322  SX=X/2:AX=X*4

# EACH "TILE" CONSISTS OF 4X2 PIXELS
# SO WE READ 4 SUBSEQUENT PIXELS, THEN 2 SUBSEQUENT PIXELS FROM THE NEXT LINE
560   I=PEEK(ID):I1=PEEK(ID+1)
570   P$(0)=MID$(HEX$(I),3,1)   :P$(1)=RIGHT$(HEX$(I),1):P$(2)=MID$(HEX$(I1),3,1):P$(3)=RIGHT$(HEX$(I1),1)

580   I=PEEK(ID+BW/2):I1=PEEK(ID+BW/2+1)
590   P$(4)=MID$(HEX$(I),3,1):P$(5)=RIGHT$(HEX$(I),1):P$(6)=MID$(HEX$(I1),3,1):P$(7)=RIGHT$(HEX$(I1),1)

#     DO WE HAVE A BLACK BACKGROUND?
600   FOR Z=0 TO 7
610    IF P$(Z)="0" THEN BB=-1:GOTO 630
620   NEXT

630   IF BB THEN C1$="0":Z0=0:ELSE C1$=P$(0):Z0=1

640   FOR Z=Z0 TO 3
650    P(Z)=P$(Z)<>C1$
660   NEXT

670   IF BB AND P(0) THEN B=192:C2$=P$(0)

680   IF P(1) THEN B=B+48:  C2$=P$(1)
690   IF P(2) THEN B=B+12:C2$=P$(2)
700   IF P(3) THEN B=B+3: C2$=P$(3)

710   VMW VR,B

# SECOND SCANLINE

720   FOR Z=4 TO 7
730    P(Z)=P$(Z)<>C1$
740   NEXT

750   B=0
760   IF P(4) THEN B=192 :C2$=P$(4)
770   IF P(5) THEN B=B+48:C2$=P$(5)
780   IF P(6) THEN B=B+12:C2$=P$(6)
790   IF P(7) THEN B=B+3: C2$=P$(7)

800   VMW VR+64,B

810   VMW VP,DEC(C1$+C2$)

820   VP=VP+1
830   VR=VR+1
840   ID=ID+2
850  NEXT
860  ID=ID+BW/2

870  VR=VR+64

880 NEXT:S=TI-S:PRINT S
890 VTR 0,DB,24000:S=TI-S:PRINT S

900 RETURN




910 DEF FN W(ZZ)=PEEK(ZZ)+PEEK(ZZ+1)*256
920 DEF FN W4(ZZ)=PEEK(ZZ)+PEEK(ZZ+1)*256+PEEK(ZZ+2)*65536+PEEK(ZZ+3)*16777216
930 DEF FN V(ZZ)=DEC(RIGHT$(HEX$(ZZ),1))
940 DEF FN HN(ZZ)=DEC(MID$(HEX$(ZZ),3,1))
950 DEF FN LN(ZZ)=DEC(RIGHT$(HEX$(ZZ),1))

960 RETURN